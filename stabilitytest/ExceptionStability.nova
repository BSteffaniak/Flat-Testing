package "stabilitytest"

import "novex/nest/Nest"
import "novex/nest/TestSuite"

import static "novex/nest/Nest"

testable class ExceptionStability {
    [Test message: false]
    public test() {
        Bool worked = false
        
        try {
            testException()
        } catch (Exception e) {
            worked = e.class == Exception // Make sure not a sub-class
        }
        
        expectTrue(worked, "Failed to catch Exception")
        
        worked = false
        
        try {
            testSoftExceptions()
        } catch all (DivideByZeroException e) {
            worked = true
            
            Console.writeLine("- Success")
        }
        
        expectTrue(worked, "Failed to catch soft Exception")
    }
    
    testException() {
        try {
            Console.write("Testing DivideByZeroException ")
            
            try {
                // Int den = 0
                // Int i   = 43 / den
                
                throw new DivideByZeroException()
            } catch (DivideByZeroException e) {
                Console.writeLine("- Success")
                Console.write("Testing StabilityTestException ")
                
                throw new StabilityTestException()
            }
            
            fail("Failed to catch DivideByZeroException")
        } catch (StabilityTestException e) {
            Console.writeLine("- Success")
            
            throw new Exception()
        }
        
        fail("Failed to catch StabilityTestException")
    }
    
    testSoftExceptions() {
        var worked = false
        
        try {
            Console.write("Testing soft exception without a soft catch ")
            
            toss new StabilityTestException()
            
            worked = true
            
            Console.writeLine("- Success")
        } catch (StabilityTestException e) {
            fail("Caught soft exception without soft keyword")
        }
        
        expectTrue(worked, "Failed to throw soft without corresponding catch")
        
        Console.write("Testing soft exception with a soft catch ")
        
        toss new DivideByZeroException()
        
        fail("Failed to exit subroutine after throwing soft Exception")
    }
}