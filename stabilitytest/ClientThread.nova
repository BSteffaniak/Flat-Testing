package "stabilitytest"

import "nova/io/OutputStream"
import "nova/network/ClientSocket"
import "nova/thread/Thread"

import "novex/nest/Nest"
import "novex/nest/TestSuite"

import static "novex/nest/Nest"

class ClientThread extends Thread {
    Int port
    
    OutputStream out
    
    public construct(private Int port, private OutputStream out) {}
    
    public run() {
        ClientSocket client = new ClientSocket()
        
        String ip = "127.0.0.1"
        
        out.write("ClientSocket attempting to connect to #ip:#{port}... ")
        
        expectTrue(client.connect(ip, port), "Failed to connect to localhost server")
        
        out.write("Success\n")
        
        out.write("Waiting for String from ServerSocket...\n")
        
        String s = client.connection.in.readString()
        
        expectTrue(s.count == NetworkStability.received.count && s == NetworkStability.received, "Client unable to receive the correct message from server")
        
        out.write("Attempting to send String to ServerSocket... ")
        
        client.connection.out.write(NetworkStability.received)
        
        out.write("Success\n")
        
        out.write("Attempting to close ClientSocket... ")
        
        expectTrue(client.close(), "Unable to close Client connection")
        
        out.write("Success\n")
    }
}