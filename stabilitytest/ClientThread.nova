package "stabilitytest"

import "nova/network/ClientSocket"
import "nova/thread/Thread"

import "novex/nest/Nest"
import "novex/nest/TestSuite"

import static "novex/nest/Nest"

class ClientThread extends Thread {
    Int port
    
    public construct(private Int port) {}
    
    public run() {
        ClientSocket client = new ClientSocket()
        
        String ip = "127.0.0.1"
        
        Console.write("ClientSocket attempting to connect to #ip:#{port}... ")
        
        expectTrue(client.connect(ip, port), "Failed to connect to localhost server")
        
        Console.writeLine("Success")
        
        Console.writeLine("Waiting for String from ServerSocket... ")
        
        String s = client.connection.in.readString()
        
        expectTrue(s.count == NetworkStability.received.count && s == NetworkStability.received, "Client unable to receive the correct message from server")
        
        Console.write("Attempting to send String to ServerSocket... ")
        
        client.connection.out.write(NetworkStability.received)
        
        Console.writeLine("Success")
        
        Console.write("Attempting to close ClientSocket... ")
        
        expectTrue(client.close(), "Unable to close Client connection")
        
        Console.writeLine("Success")
    }
}