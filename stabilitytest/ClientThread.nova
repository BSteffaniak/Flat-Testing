package "stabilitytest"

import "nova/network/ClientSocket"
import "nova/thread/Thread"

class ClientThread extends Thread {
    Int port
    
    StabilityTest program
    
    public construct(StabilityTest program, Int port) {
        this.program = program
        this.port    = port
    }
    
    public run() {
        ClientSocket client = new ClientSocket()
        
        String ip = "127.0.0.1"
        
        Console.write("ClientSocket attempting to connect to #ip:#{port}... ")
        
        program.fail("Failed to connect to localhost server", !client.connect(ip, port))
        
        Console.writeLine("OK")
        
        Console.writeLine("Waiting for String from ServerSocket... ")
        
        String s = client.connection.in.readString()
        
        program.fail("Client unable to receive the correct message from server", s.count != NetworkStability.received.count || !s.equals(NetworkStability.received))
        
        Console.write("Attempting to send String to ServerSocket... ")
        
        client.connection.out.write(NetworkStability.received)
        
        Console.writeLine("OK")
        
        Console.write("Attempting to close ClientSocket... ")
        
        program.fail("Unable to close Client connection", !client.close())
        
        Console.writeLine("OK")
    }
}