package "stabilitytest"

import "nova/io/File"
import "nova/io/FileReader"
import "nova/io/FileWriter"
import "nova/time/Time"

class FileStability extends StabilityTestCase {
    static String inputString
    
    static Int lines
    
    public construct(StabilityTest program) => super(program)
    
    public test() {
        Console.writeLine("Checking File IO...")
        
        inputString = "This is test input..."
        lines       = 100
        
        try {
            File f = new File("temp#Time.currentTimeMillis")
            
            var writer = new FileWriter(f)
            var reader = new FileReader(f)
            
            createFile(writer)
            writeToFile(writer)
            readFromFile(reader)
            deleteFile(writer)
        } catch (Exception e) {
            program.fail("Failed; Exception thrown")
        }
    }
    
    createFile(FileWriter writer) {
        Console.write("Creating file... ")
        
        program.fail("Failed to create file", !writer.create())
        
        Console.writeLine("OK")
    }
    
    writeToFile(FileWriter writer) {
        Console.write("Writing #{lines} lines of data to file... ")
        
        for (i in 0..lines) {
            writer.writeLine(inputString)
        }
        
        Console.writeLine("OK")
    }
    
    readFromFile(FileReader reader) {
        Console.write("Reading lines from file... ")
        
        if (!reader.isOpen) {
            reader.open()
        }
        
        program.fail("File reader failed to open", !reader.isOpen)
        
        Int    times = 0
        String line  = reader.readLine()
        
        while (line) {
            program.fail("Reading from file failed: '#line' != '#inputString'", !line.equals(inputString))
            
            line = reader.readLine()
            
            times++
        }
        
        reader.close()
        
        program.fail("Failed; only read #{times}/100 lines", times != lines)
        
        Console.writeLine("OK")
    }
    
    deleteFile(FileWriter writer) {
        Console.write("Deleting file... ")
        
        program.fail("Failed to delete file", !writer.delete())
        
        Console.writeLine("OK")
    }
}
