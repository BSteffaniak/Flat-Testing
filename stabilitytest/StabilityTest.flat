package "stabilitytest"

import "flatlang/time/Timer"
import "flatlang/thread/Thread"
import "flatlang/io/File"
import "flatlang/io/FancyOutputStream"
import "flatlang/star/Button"
import "flatlang/star/Label"
import "flatlang/star/Panel"
import "flatlang/star/Color"
import "flatlang/star/ScrollBar"

import "novex/nest/ui/ResultWindow"
import "novex/nest/ui/ResultBar"
import "novex/nest/TestResult"

[TestSuite [
    ExceptionStability,
    SyntaxStability,
    ClosureStability,
    PolymorphismStability,
    LambdaStability,
    ToStringStability,
    AssignmentStability,
    StaticImportStability,
    InnerClassStability,
    ExternalInnerClassStability,
    PrimitiveOverloadStability,
    FancyOutputStreamTests,
    FirstClassFunctionStability,
    LibraryLoadingStability,
    IntervalStability,
    BoundedIntervalStability,
    BoundlessIntervalStability,
    NetworkStability,
    CastStability
]]
[Target c]
[TestSuite [
    TimeStability,
    ThreadStability,
    FileStability,
    RegexStability
]]
class StabilityTest {
    public static async main(String args[]) {
        let test = new StabilityTest()

        Timer timer = new Timer().start()

        let results = new TestResult[]

        let suites = args.skip(2)

        if (suites.count > 0) {
            suites.forEach({
                native TestResult[] suiteResults
                var count = 0

                external js {
                    #{suiteResults} = [];

                    var instance = #{System.jsCreateInstance(_)}

                    instance.runTests((r) => {
                        #{suiteResults}.push(r);
                        #{count}++;
                    });
                }

                for (i in 0..count) {
                    results.add(suiteResults[i])
                }
            })
        } else {
            await test.runTests({
                results.add(_)
            })
        }

        let fancy = new FancyOutputStream(headerPattern: "/\\")

        let unsuccessful = results.filter({ !_.success })

        if (unsuccessful.count > 0) {
            fancy.writeHeader("#unsuccessful.count Failure#{unsuccessful.count == 1 ? "" : "s"} out of #results.count test#{results.count == 1 ? "" : "s"}", pattern: ":'( ", symmetrical: true)

            unsuccessful.forEach({
                Console.log("#_")
            })
        } else {
            fancy.writeHeader("All #results.count Successful", pattern: ":) ", symmetrical: true)
        }

        timer.stop()

        Console.writeLine("Took " + timer.duration + "ms")

        if (unsuccessful.count > 0) {
            System.exit(1)
        }
    }
}